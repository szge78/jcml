plugins {
    id("groovy") 
    id("io.micronaut.application") version "4.6.1"
    id("com.gradleup.shadow") version "8.3.9"
//    id("io.micronaut.test-resources") version "4.6.1"
    id("io.micronaut.aot") version "4.6.1"
}

version = "0.3.20260223.0932"
group = "sk.concentra.jcml"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micrometer:context-propagation")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.reactor:micronaut-reactor")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    compileOnly("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.microsoft.sqlserver:mssql-jdbc")
    runtimeOnly("org.yaml:snakeyaml")
    testImplementation("io.micronaut:micronaut-http-client")
    // commons-compiler - Janino - conditional logback:
    implementation 'org.codehaus.janino:janino:3.1.12'
    // Conditions:
    implementation 'com.octomix.josson:josson:1.5.1'
    // Micrometer:
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micronaut.micrometer:micronaut-micrometer-core'

    // Reactor integration:
//    implementation("io.micronaut:micronaut-inject")
//    implementation("io.micronaut:micronaut-runtime")
//    implementation("io.micronaut.reactor:micronaut-reactor")
    // @Refreshable:
    implementation("io.micronaut:micronaut-management")
    // CXF:
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation 'org.apache.cxf:cxf-rt-transports-http-netty-server:4.0.4'
    implementation 'org.apache.cxf:cxf-rt-frontend-jaxws:4.0.4'
    implementation 'org.apache.cxf:cxf-rt-transports-http:4.0.4'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    runtimeOnly    'com.sun.xml.bind:jaxb-impl:4.0.5'
}


application {
    mainClass = "sk.concentra.jcml.Application"
}
java {
    sourceCompatibility = JavaVersion.toVersion("25")
    targetCompatibility = JavaVersion.toVersion("25")
}


graalvmNative.toolchainDetection = false

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("sk.concentra.jcml.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = false
    }
}


tasks.named("shadowJar") {
    // CXF registers Bus extensions via META-INF/cxf/bus-extensions.txt.
    // Multiple CXF JARs each supply this file; Shadow must append them, not overwrite.
    // Without this, Bus.getExtension(ServerRegistry.class) returns null at runtime.
    mergeServiceFiles()
    append('META-INF/cxf/bus-extensions.txt')
}

tasks.named("dockerfileNative") {
    jdkVersion = "25"
}

run {
    jvmArgs("-Dmicronaut.environments=prod",
            "-Dcom.sun.management.jmxremote",
            "-Dcom.sun.management.jmxremote.port=10999",
            "-Dcom.sun.management.jmxremote.authenticate=false",
            "-Dcom.sun.management.jmxremote.ssl=false",
            '-Djava.util.concurrent.ForkJoinPool.common.parallelism=4',
            '-DsocksProxyHost=winutil0',
            '-DsocksProxyPort=7777',
            '-DsocksNonProxyHosts=arcturus',
            "-Dmicronaut.config.files=${System.getProperty("user.home")}/configs/jcml.yml")
} // run {

test {
    testLogging {
        // This sends output to the console/terminal
        showStandardStreams = true

        // Optional: Make it prettier
        events "passed", "skipped", "failed"
    }

    jvmArgs("-Dmicronaut.environments=prod",
            "-Dcom.sun.management.jmxremote",
            "-Dcom.sun.management.jmxremote.port=10999",
            "-Dcom.sun.management.jmxremote.authenticate=false",
            "-Dcom.sun.management.jmxremote.ssl=false",
            '-Dcom.sun.xml.bind.v2.verboseProcessing=true',
            '-Djava.util.concurrent.ForkJoinPool.common.parallelism=16',
            '-DsocksProxyHost=winutil0',
            '-DsocksProxyPort=7777',
            '-DsocksNonProxyHosts=arcturus',
            '-Xmx8g',
            '-XX:+HeapDumpOnOutOfMemoryError',
            "-Dmicronaut.config.files=${System.getProperty("user.home")}/configs/jcml.yml")
}
